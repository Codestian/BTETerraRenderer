plugins {
    id 'net.minecraftforge.gradle'
    id 'btr.java-conventions'
    id 'org.spongepowered.mixin'
}

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: "${project.mappingsVersion}"

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }
        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES, REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }
    }
}

repositories {
    mavenCentral()
    // cubicchunks, cubicworldgen, terraplusplus
    maven { name = "JitPack"; url = "https://jitpack.io/" }
    // jankson
    maven { name = "elytradev"; url = "https://repo.elytradev.com/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"

    shadeCore fg.deobf(project(':btr-core'))

    annotationProcessor fg.deobf('org.spongepowered:mixin:0.8.5:processor')
    testImplementation fg.deobf('org.spongepowered:lwts:1.0.0')
    testImplementation fg.deobf('org.spongepowered:mixin:0.8.5')
    testAnnotationProcessor fg.deobf('org.spongepowered:mixin:0.8.5:processor')

    compileOnly fg.deobf('com.github.OpenCubicChunks.CubicChunks:cubicchunks:f71aafb9854466ac5ffa0ccd2796921684f1d8b1') { transitive = false }
    compileOnly fg.deobf('com.github.OpenCubicChunks:CubicWorldGen:27de56d2f792513873584b2f8fd9f3082fb259ec') { transitive = false }
    compileOnly fg.deobf('com.github.BuildTheEarth:terraplusplus:f12de26053')
    testImplementation fg.deobf('com.github.BuildTheEarth:terraplusplus:f12de26053')
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraftVersion

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraftVersion
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

reobf {
    shadowJar {}
}
shadowJar.configure { dependsOn("reobfJar") }

mixin {
    add sourceSets.main, 'mixins.bteterrarenderer.refmap.json'
    config 'mixins.bteterrarenderer.json'
}

jar {
    manifest.attributes(
            "MixinConfigs": 'mixins.bteterrarenderer.json',
            "ForceLoadAsMod": 'true',
            "TweakClass": 'org.spongepowered.asm.launch.MixinTweaker',
            "TweakOrder": 0,
            "Manifest-Version": 1.0
    )
}